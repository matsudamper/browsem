plugins {
    alias(libs.plugins.android.application)
    alias(libs.plugins.kotlin.compose)
    alias(libs.plugins.kotlin.serialization)
}

def ciDebugKeystorePath = System.getenv("DEBUG_KEYSTORE_PATH")
def ciDebugKeystoreFile = ciDebugKeystorePath ? file(ciDebugKeystorePath) : null
def useCiDebugKeystore = ciDebugKeystoreFile != null && ciDebugKeystoreFile.exists()

android {
    namespace 'net.matsudamper.browser'
    compileSdk 36

    defaultConfig {
        applicationId "net.matsudamper.browser"
        minSdk 26
        targetSdk 36
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        vectorDrawables {
            useSupportLibrary true
        }
    }

    signingConfigs {
        if (useCiDebugKeystore) {
            debugCi {
                storeFile ciDebugKeystoreFile
                storePassword System.getenv("DEBUG_KEYSTORE_PASSWORD") ?: "android"
                keyAlias System.getenv("DEBUG_KEY_ALIAS") ?: "androiddebugkey"
                keyPassword System.getenv("DEBUG_KEY_PASSWORD") ?: "android"
            }
        }
    }

    buildTypes {
        debug {
            if (useCiDebugKeystore) {
                signingConfig signingConfigs.debugCi
            }
        }

        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            if (useCiDebugKeystore) {
                signingConfig signingConfigs.debugCi
            }
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_21
        targetCompatibility JavaVersion.VERSION_21
    }

    buildFeatures {
        compose true
    }

    packaging {
        resources {
            excludes += '/META-INF/{AL2.0,LGPL2.1}'
        }
    }
}

kotlin {
    compilerOptions {
        jvmTarget = org.jetbrains.kotlin.gradle.dsl.JvmTarget.JVM_21
    }
}

dependencies {
    implementation platform(libs.androidx.compose.bom)
    implementation libs.androidx.activity.compose
    implementation libs.androidx.compose.material3
    implementation libs.androidx.compose.ui
    implementation libs.androidx.compose.ui.tooling.preview
    implementation libs.androidx.navigation3.runtime
    implementation libs.androidx.navigation3.ui
    implementation libs.mozilla.geckoview

    debugImplementation libs.androidx.compose.ui.tooling
    debugImplementation libs.androidx.compose.ui.test.manifest

    testImplementation libs.junit4
    androidTestImplementation platform(libs.androidx.compose.bom)
    androidTestImplementation libs.androidx.test.ext.junit
    androidTestImplementation libs.androidx.test.espresso.core
    androidTestImplementation libs.androidx.compose.ui.test.junit4
}
